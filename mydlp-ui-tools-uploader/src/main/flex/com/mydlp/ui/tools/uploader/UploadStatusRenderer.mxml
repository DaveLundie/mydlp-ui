<?xml version="1.0" encoding="utf-8"?>
<s:ItemRenderer xmlns:fx="http://ns.adobe.com/mxml/2009" 
				xmlns:s="library://ns.adobe.com/flex/spark" 
				xmlns:mx="library://ns.adobe.com/flex/mx" 
				autoDrawBackground="true" xmlns:local="*"
				width="100%"
				height="30"
				currentState="@{uploadStatus.viewState}" xmlns:uploader="com.mydlp.ui.tools.uploader.*"
				>
	<fx:Script>
		<![CDATA[
			import mx.core.FlexGlobals;
			
			private static const CHUNK_SIZE:Number=512*1024;
			
			override public function set data(value:Object):void
			{
				if (value is UploadStatus)
				{
					uploadStatus = value as UploadStatus;
					super.currentState = uploadStatus.viewState;
					super.data = value;
				}
				else
				{
					super.data = null;
					return;
				}
				if (uploadStatus.viewState == "init")
				{
					start();
				}
			}
			
			protected function start(): void
			{
				uploadStatus.bytesTotal = uploadStatus.file.size;
				uploadStatus.name = uploadStatus.file.name;
				startUpload();
			}
			
			protected function startUpload(): void
			{
				configureListenersFile(uploadStatus.file);
				
				// Create the request URL based on the download URL
				var requestURL:URLRequest = new URLRequest(
					FlexGlobals.topLevelApplication.tokenMode + "://" + 
					FlexGlobals.topLevelApplication.tokenHost + "/tools/uploader");
				requestURL.method = URLRequestMethod.POST;
				
				// Set the post parameters
				var params:URLVariables = new URLVariables(); 
				params.fileName = uploadStatus.name;
				params.tokenKey = FlexGlobals.topLevelApplication.tokenKey;
				requestURL.data = params;
				
				// Start uploading the file to the server
				uploadStatus.file.upload(requestURL, "fileData");
				currentState="uploading";
			}
			
			protected function uploadCompleteDataHandler(event:DataEvent): void
			{
				uploadStatus.resetData();
				trace("RESP: '" + event.data + "'");
				if (event.data == null || event.data.length == 0)
				{
					currentState="failed";
					uploadStatus.serverReason = "Empty response";
					FlexGlobals.topLevelApplication.fileUploadFailed();
				} 
				else if (event.data == "ok")
				{
					currentState="finished";
					FlexGlobals.topLevelApplication.fileUploadFinished();
				}
				else if (event.data == "exists")
				{
					currentState="skipped";
					uploadStatus.serverReason = "File already exists";
					FlexGlobals.topLevelApplication.fileUploadSkipped();
				}
				else if (event.data == "error")
				{
					currentState="failed";
					uploadStatus.serverReason = "Server sent error message";
					FlexGlobals.topLevelApplication.fileUploadFailed();
				}
				else
				{
					currentState="failed";
					uploadStatus.serverReason = "Improper response";
					FlexGlobals.topLevelApplication.fileUploadFailed();
				}
			}
			
			protected function errorHandler(event:Event): void
			{
				uploadStatus.resetData();
				uploadStatus.serverReason = "IO Error";
				trace("errorHandler: " + event.toString());
				currentState="failed";
				FlexGlobals.topLevelApplication.fileUploadFailed();
			}
			
			private function configureListenersFile(dispatcher:IEventDispatcher):void {
				dispatcher.addEventListener(Event.CANCEL, cancelHandler);
				dispatcher.addEventListener(Event.COMPLETE, completeHandler);
				dispatcher.addEventListener(HTTPStatusEvent.HTTP_STATUS, errorHandler);
				dispatcher.addEventListener(IOErrorEvent.IO_ERROR, errorHandler);
				dispatcher.addEventListener(Event.OPEN, openHandler);
				dispatcher.addEventListener(SecurityErrorEvent.SECURITY_ERROR, errorHandler);
				dispatcher.addEventListener(DataEvent.UPLOAD_COMPLETE_DATA, uploadCompleteDataHandler);
			}
			
			private function cancelHandler(event:Event):void {
				trace("cancelHandler: " + event);
				uploadStatus.resetData();
				currentState="failed";
				uploadStatus.serverReason = "Canceled";
				FlexGlobals.topLevelApplication.fileUploadFailed();
			}
			
			private function completeHandler(event:Event):void {
				trace("completeHandler: " + event);
			}
			
			private function openHandler(event:Event):void {
				trace("openHandler: " + event);
			}
			
		]]>
	</fx:Script>
	
	<fx:Declarations>
		<uploader:UploadStatus id="uploadStatus" />
	</fx:Declarations>
	
	<s:states>
		<s:State name="init" />
		<s:State name="uploading" />
		<s:State name="failed" />
		<s:State name="skipped" />
		<s:State name="finished" />
	</s:states>
	
	<s:HGroup verticalAlign="middle" width="100%" height="100%" gap="20">
		
		<s:Label width="100%" height="100%"
				 text.init="Starting..."
				 text.uploading="{'Uploading... (' + uploadStatus.name + ', ' + uploadStatus.bytesTotal + ' bytes)'}"
				 text.failed="{'Failed! (' + uploadStatus.name + ') : ' + uploadStatus.serverReason}"
				 text.skipped="{'Skipped. (' + uploadStatus.name + ') : ' + uploadStatus.serverReason}"
				 text.finished="{'Finished! ' + uploadStatus.bytesTotal + ' bytes are uploaded. (' + uploadStatus.name + ')'}"
				 />
		
		<mx:ProgressBar id="progressBar"
						width="200"
						height="100%"
						includeIn="uploading"
						mode="event"
						source.uploading="{uploadStatus.file}"
						label.uploading="UPLOADING %%%3"
						/>
		
	</s:HGroup>
	
</s:ItemRenderer>
