<?xml version="1.0" encoding="utf-8"?>
<general:GenericCRUDDialog xmlns:fx="http://ns.adobe.com/mxml/2009" 
				xmlns:s="library://ns.adobe.com/flex/spark" 
				xmlns:mx="library://ns.adobe.com/flex/mx"
				xmlns:general="com.mydlp.ui.widget.general.*"
				xmlns:my="http://www.mydlp.com/flex/my"
				>
	
	<fx:Script>
		<![CDATA[
			import com.mydlp.ui.domain.AbstractEntity;
			import com.mydlp.ui.domain.InventoryBase;
			import com.mydlp.ui.domain.InventoryCategory;
			import com.mydlp.ui.domain.InventoryItem;
			import com.mydlp.ui.domain.Item;
			import com.mydlp.ui.util.ClassMember;
			import com.mydlp.ui.util.InteractionUtil;
			import com.mydlp.ui.util.LangUtil;
			import com.mydlp.ui.util.ReflectionUtil;
			import com.mydlp.ui.widget.general.input.IpAddressInput;
			
			import flash.utils.getQualifiedClassName;
			
			import flashx.textLayout.container.ISandboxSupport;
			
			import mx.binding.utils.BindingUtils;
			import mx.collections.ListCollectionView;
			import mx.controls.Alert;
			import mx.core.FlexGlobals;
			import mx.core.IVisualElement;
			import mx.events.FlexEvent;
			
			import spark.components.Button;
			import spark.components.FormItem;
			import spark.components.TextInput;
			
			protected var cButton:Button = new Button();
			
			public function populateClassMembers(targetObject:Object): void
			{
				var classMembers:Array = ReflectionUtil.getClassMembers(targetObject as Object);
				
				for each (var classMember:ClassMember in classMembers)
				{
					var visualElement:IVisualElement = null;
					
					if (classMember.name == "category" && classMember.type == InventoryCategory)
					{
						if (targetObject.id == null || isNaN(targetObject.id) ) // means that this is a new object
							if (FlexGlobals.topLevelApplication.inventoryTree.selectedItem == null)
								continue;
							else if (FlexGlobals.topLevelApplication.inventoryTree.selectedItem is InventoryCategory)
								targetObject[classMember.name] = FlexGlobals.topLevelApplication.inventoryTree.selectedItem;
							else if (FlexGlobals.topLevelApplication.inventoryTree.selectedItem is InventoryBase)
								targetObject[classMember.name] = FlexGlobals.topLevelApplication.inventoryTree.selectedItem.category;
						continue;
					}
					else if 
						(
							classMember.name == "id" ||
							classMember.name == "nameKey" ||
							(classMember.name == "children" && classMember.type == ListCollectionView)||
							(classMember.name == "coupledInventoryItem" && classMember.type == InventoryItem) ||
							(classMember.name == "coupledRuleItems" && classMember.type == ListCollectionView)
						)
					{
						continue;
					}
					else if (targetObject is InventoryItem && classMember.name == "item")
					{
						if (targetObject.item != null && targetObject.item is Item)
							(targetObject.item as Item).coupledInventoryItem = targetObject as InventoryItem;
						populateClassMembers(targetObject.item);
						continue;
					}
					else if (
							( classMember.name == "ipBase" || classMember.name == "ipMask" )
							&& classMember.type == Number)
					{
						var ipInput:IpAddressInput = new IpAddressInput();
						ipInput.numberValue = targetObject[classMember.name];
						BindingUtils.bindProperty(targetObject, classMember.name, ipInput, "numberValue");
						visualElement = ipInput;
					}
					else
					{
						var textInput:TextInput = new TextInput();
						textInput.text = targetObject[classMember.name];
						BindingUtils.bindProperty(targetObject, classMember.name, textInput, "text");
						visualElement = textInput;
					}
					
					if (visualElement != null)
					{
						var fi:FormItem = new FormItem();
						fi.label = LangUtil.getString("messages", "generic.edit." + getObjectName(targetObject) + "." + classMember.name + ".label");
						fi.addElement(visualElement);
						form.addElement(fi);
					}
				}
			}
			
			public function populateCreateButton(): void
			{	
				cButton.label = LangUtil.getString("messages", "generic.edit." + getSimpleName() + ".createButton.label");
				cButton.addEventListener(MouseEvent.CLICK, createButtonClickHandler);
				var cbfi:FormItem = new FormItem();
				cbfi.addElement(cButton);
				form.addElement(cbfi);
			}
				
			
			override public function populate(): void
			{
				title = LangUtil.getString("messages", "generic.edit." + getSimpleName() + ".title");
				
				populateClassMembers(formObject);
				
				populateCreateButton();
				
				super.populate();
			}
			
			protected function createButtonClickHandler(event:Event): void
			{
				re.save(formObject);
				cButton.enabled = false;
			}
			
			protected function okHandler(event:Event): void
			{
				InteractionUtil.closePopup(this);
				Alert.show(	LangUtil.getString("messages", "generic.edit." + getSimpleName() + ".ok.message"),
							LangUtil.getString("messages", "generic.edit." + getSimpleName() + ".title"));
				
				FlexGlobals.topLevelApplication.refresh();
			}
			
		]]>
	</fx:Script>
	
	<fx:Declarations>
		<my:remote id="re" destination="genericBRS" >
			<my:method name="save" result="okHandler(event)" />
		</my:remote>
	</fx:Declarations>
	
	<s:Form id="form" />
	
</general:GenericCRUDDialog>
