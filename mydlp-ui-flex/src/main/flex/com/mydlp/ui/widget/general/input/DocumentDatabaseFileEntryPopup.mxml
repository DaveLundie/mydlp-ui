<?xml version="1.0" encoding="utf-8"?>
<s:TitleWindow xmlns:fx="http://ns.adobe.com/mxml/2009" 
			   xmlns:s="library://ns.adobe.com/flex/spark" 
			   xmlns:mx="library://ns.adobe.com/flex/mx"
			   xmlns:input="com.mydlp.ui.widget.general.input.*"
			   close="{close()}"
			   visible="false"
			   title="@Resource(bundle = 'messages', key = 'documentDatabase.add.title.label')"
			   >
	
	
	<fx:Script>
		<![CDATA[
			import com.mydlp.ui.domain.DocumentDatabaseFileEntry;
			import com.mydlp.ui.util.InteractionUtil;
			import com.mydlp.ui.util.Md5Util;
			
			import mx.collections.ArrayCollection;
			import mx.controls.Alert;
			import mx.events.DragEvent;
			import mx.events.FlexEvent;
			import mx.managers.DragManager;
			import mx.rpc.events.ResultEvent;
			
			protected var refUploadFileList:FileReferenceList;
			
			public var ddfeInput:DocumentDatabaseFileEntryInput = null;
			
			protected var remaining:int = 0;
			
			protected function close(): void
			{
				if (okButton.visible)
					InteractionUtil.closePopup(this);
			}
			
			protected function uploadButton(event:MouseEvent):void
			{
				refUploadFileList = new FileReferenceList();
				refUploadFileList.browse(); 
				refUploadFileList.addEventListener(Event.SELECT,onFileSelect); 
			}
			
			protected function loadEachFile(file:FileReference):void
			{
				var fileLoad:FileLoadStatus = new FileLoadStatus();
				fileLoad.databaseId = ddfeInput.databaseId;
				loadStatusList.addElement(fileLoad);
				fileLoad.loadFile(file, onFileLoadComplete);
			}
			
			protected function onFileSelect(event:Event):void
			{
				refUploadFileList = event.currentTarget as FileReferenceList;
				var fileListArray:Array = refUploadFileList.fileList;
				
				if (loadStatusList.numElements > 25)
					loadStatusList.removeAllElements();
				
				remaining += fileListArray.length;
				if (remaining > 0)
				{
					okButton.enabled = false;
					browseButton.enabled = false;
				}
				
				for(var i:int = 0; i < fileListArray.length; i++)
				{
					var file:FileReference = fileListArray[i] as FileReference;
					loadEachFile(file);
				}
			}
			
			protected function onFileLoadComplete(ddfe:DocumentDatabaseFileEntry):void
			{
				ddfeInput.addEntry(ddfe);
				remaining --;
				if (remaining == 0)
				{
					okButton.enabled = true;
					browseButton.enabled = true;
				}
			}
			
			public function populate():void
			{				
				visible = true;
			}
			
			
		]]>
	</fx:Script>
	<s:layout>
		<s:VerticalLayout paddingTop="15" paddingBottom="15"
						  paddingLeft="10" paddingRight="10" 
						  />
	</s:layout>
	<s:VGroup id="loadStatusList" gap="5" paddingBottom="10" horizontalAlign="center" width="100%"/>
	
	<s:HGroup width="300" 
			  horizontalAlign="center" gap="20" verticalAlign="middle" >
		
		<s:Button id="browseButton" label="@Resource(key='documentDatabase.fileUpload.browse', bundle='messages')"
				  click="uploadButton(event)" />
					
		<s:Button id="okButton" label="@Resource(bundle = 'messages', key = 'documentDatabase.button.ok.label')" 
				  click="{close()}"/>
		
	</s:HGroup>
	
</s:TitleWindow>
