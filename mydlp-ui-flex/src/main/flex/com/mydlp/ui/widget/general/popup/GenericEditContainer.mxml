<?xml version="1.0" encoding="utf-8"?>
<popup:GenericCRUDContainer xmlns:fx="http://ns.adobe.com/mxml/2009" 
				xmlns:s="library://ns.adobe.com/flex/spark" 
				xmlns:mx="library://ns.adobe.com/flex/mx"
				xmlns:general="com.mydlp.ui.widget.general.*"
				xmlns:my="http://www.mydlp.com/flex/my" 
				xmlns:popup="com.mydlp.ui.widget.general.popup.*"
				width="400"
				>
	
	<fx:Script>
		<![CDATA[
			import com.mydlp.ui.domain.AbstractEntity;
			import com.mydlp.ui.domain.InformationDescription;
			import com.mydlp.ui.domain.InformationType;
			import com.mydlp.ui.domain.InventoryBase;
			import com.mydlp.ui.domain.InventoryCategory;
			import com.mydlp.ui.domain.InventoryItem;
			import com.mydlp.ui.domain.Item;
			import com.mydlp.ui.util.ClassMember;
			import com.mydlp.ui.util.InteractionUtil;
			import com.mydlp.ui.util.LangUtil;
			import com.mydlp.ui.util.ReflectionUtil;
			import com.mydlp.ui.widget.general.input.DataFormatInput;
			import com.mydlp.ui.widget.general.input.InformationFeatureInput;
			import com.mydlp.ui.widget.general.input.IpAddressInput;
			
			import flash.utils.getQualifiedClassName;
			
			import flashx.textLayout.container.ISandboxSupport;
			
			import mx.binding.utils.BindingUtils;
			import mx.collections.ArrayCollection;
			import mx.collections.ListCollectionView;
			import mx.controls.Alert;
			import mx.core.FlexGlobals;
			import mx.events.FlexEvent;
			
			import spark.components.Button;
			import spark.components.CheckBox;
			import spark.components.TextInput;
			
			protected var cButton:Button = new Button();
			protected var formItemOrder:ListCollectionView = new ArrayCollection();
			
			public var deleteList:ListCollectionView;
			
			protected var sourceLabelList:Array = new Array();
			
			public function populateClassMembers(targetObject:Object): void
			{
				var classMembers:Array = ReflectionUtil.getClassMembers(targetObject as Object);
				var index:Number = -1;
				
				for each (var classMember:ClassMember in classMembers)
				{
					var inputLabel:InputLabel = null;
					
					if (classMember.name == "category" && classMember.type == InventoryCategory)
					{
						if (targetObject.id == null || isNaN(targetObject.id) ) // means that this is a new object
							if (FlexGlobals.topLevelApplication.inventoryTree.selectedItem == null)
								continue;
							else if (FlexGlobals.topLevelApplication.inventoryTree.selectedItem is InventoryCategory)
								targetObject[classMember.name] = FlexGlobals.topLevelApplication.inventoryTree.selectedItem;
							else if (FlexGlobals.topLevelApplication.inventoryTree.selectedItem is InventoryBase)
								targetObject[classMember.name] = FlexGlobals.topLevelApplication.inventoryTree.selectedItem.category;
						continue;
					}
					else if 
						(
							classMember.name == "id" ||
							classMember.name == "optimisticLockVersion" ||
							classMember.name == "nameKey" ||
							classMember.name == "icon" ||
							(classMember.name == "ruleItems" && classMember.type == ListCollectionView) ||
							classMember.name == "enabled" ||
							classMember.name == "priority" ||
							classMember.name == "action" ||
							(classMember.name == "children" && classMember.type == ListCollectionView)||
							(classMember.name == "roles" && classMember.type == ListCollectionView)||
							(classMember.name == "coupledInventoryItem" && classMember.type == InventoryItem) ||
							(classMember.name == "coupledRuleItems" && classMember.type == ListCollectionView) ||
							(classMember.name == "informationType" && classMember.type == InformationType) 
						)
					{
						continue;
					}
					else if (targetObject is InventoryItem && classMember.name == "item")
					{
						if (targetObject.item != null && targetObject.item is Item)
							(targetObject.item as Item).coupledInventoryItem = targetObject as InventoryItem;
						populateClassMembers(targetObject.item);
						continue;
					}
					else if (
							( classMember.name == "ipBase" || classMember.name == "ipMask" )
							&& classMember.type == Number)
					{
						var ipInput:IpAddressInput = new IpAddressInput();
						ipInput.numberValue = targetObject[classMember.name];
						BindingUtils.bindProperty(targetObject, classMember.name, ipInput, "numberValue");
						ipInput.percentWidth = 100;
						inputLabel = new HorizontalInputLabel()
						inputLabel.addElement(ipInput);
						
						if(classMember.name == "ipBase")
							index = 2;
						else
							index = 1;
					}
					else if (targetObject is InformationType && 
							classMember.name == "dataFormats" && 
							classMember.type == ListCollectionView)
					{
						var dfInput:DataFormatInput = new DataFormatInput();
						if (targetObject[classMember.name] == null)
							dfInput.dataFormats = new ArrayCollection();
						else
							dfInput.dataFormats = targetObject[classMember.name];
						BindingUtils.bindProperty(targetObject, classMember.name, dfInput, "dataFormats");
						inputLabel = new VerticalInputLabel()
						inputLabel.addElement(dfInput);
						index = 8;
					}
					else if (targetObject is InformationDescription &&
							classMember.name == "features" && 
							classMember.type == ListCollectionView)
					{
						var ifInput:InformationFeatureInput = new InformationFeatureInput();
						ifInput.deleteList = this.deleteList;
						if (targetObject[classMember.name] == null)
							ifInput.informationFeatures = new ArrayCollection();
						else
							ifInput.informationFeatures = targetObject[classMember.name];
						BindingUtils.bindProperty(targetObject, classMember.name, ifInput, "informationFeatures");
						inputLabel = new VerticalInputLabel()
						inputLabel.addElement(ifInput);
						index = 9;
					}
					else if (targetObject is InformationType &&
							classMember.name == "informationDescription" && 
							classMember.type == InformationDescription)
					{
						if (targetObject[classMember.name] == null)
							targetObject[classMember.name] = new InformationDescription();
						
						populateClassMembers(targetObject[classMember.name]);
						continue;
					}
					
					else if(classMember.type == Boolean)
					{
						var checkBox:CheckBox = new CheckBox();
						checkBox.selected = targetObject[classMember.name];
						BindingUtils.bindProperty(targetObject, classMember.name, checkBox, "selected");
						inputLabel = new HorizontalInputLabel()
						inputLabel.addElement(checkBox);
						index = 6;
					}
					else
					{
						var textInput:TextInput = new TextInput();
						textInput.text = targetObject[classMember.name];
						BindingUtils.bindProperty(targetObject, classMember.name, textInput, "text");
						textInput.percentWidth = 100;
						inputLabel = new HorizontalInputLabel()
						inputLabel.addElement(textInput);
						if(classMember.name == "name"){
							index = 0;
						}
						else if(classMember.name == "username"){
							index = 3;
						}
						else if(classMember.name == "email"){
							index = 4;
						}
						else if(classMember.name == "password"){
							index = 5;
						}
						else if(classMember.name == "threshold"){
							index = 7;
						}
					}
					
					if (inputLabel != null)//index tuttum ama gerek yok sanirim, direk burada edip string olarak tutarim!
					{
						inputLabel.label = LangUtil.getString("messages", "generic.edit." + getObjectName(targetObject) + "." + classMember.name + ".label");
						sourceLabelList.push(new InputLabelIndexTuple(index, inputLabel));
					}
				}
			}
			
			public function populateEachFormItem():void
			{
				sourceLabelList.sortOn("order", Array.DESCENDING, Array.NUMERIC);
				for(var i:int = sourceLabelList.length-1; i >= 0; i--){
					var tempElement:InputLabelIndexTuple = sourceLabelList[i] as InputLabelIndexTuple;
					if(tempElement != null){
						var tempInputLabel:InputLabel = tempElement.vs;
						editForm.addElement(tempInputLabel);
					}
				}
			}
			public function populateCreateButton(): void
			{	
				cButton.label = LangUtil.getString("messages", "generic.edit." + getSimpleName() + ".createButton.label");
				cButton.addEventListener(MouseEvent.CLICK, createButtonClickHandler);
				var cbil:HorizontalInputLabel = new HorizontalInputLabel();
				cbil.addElement(cButton);
				editForm.addElement(cbil);
			}
				
			
			override public function populate(): void
			{
				deleteList = new ArrayCollection();
				
				windowTitle = LangUtil.getString("messages", "generic.edit." + getSimpleName() + ".title");
				
				populateClassMembers(formObject);
				
				populateEachFormItem();
				
				populateCreateButton();
				
				super.populate();
			}
			
			protected function createButtonClickHandler(event:Event): void
			{
				re.save(formObject);
				cButton.enabled = false;
			}
			
			protected function okHandler(event:Event): void
			{
				InteractionUtil.closeCurrentPopup();
				Alert.show(	LangUtil.getString("messages", "generic.edit." + getSimpleName() + ".ok.message"),
							LangUtil.getString("messages", "generic.edit." + getSimpleName() + ".title"));
				
				if (deleteList.length == 0)
					FlexGlobals.topLevelApplication.refresh();
				else
					re.removeAll(deleteList);
			}
			
			protected function removeAllHandler(event:Event): void
			{
				FlexGlobals.topLevelApplication.refresh();
			}
			
		]]>
	</fx:Script>
	
	<fx:Declarations>
		<my:remote id="re" destination="genericBRS" >
			<my:method name="save" result="okHandler(event)" />
			<my:method name="removeAll" result="removeAllHandler(event)" />
		</my:remote>
	</fx:Declarations>
	
	<s:Group width="100%" height="100%">
		<s:layout>
			<s:VerticalLayout horizontalAlign="center" verticalAlign="middle" />
		</s:layout>
		<s:Group id="editForm" width="100%" height="100%">
			<s:layout>
				<s:VerticalLayout horizontalAlign="left" verticalAlign="middle"/>
			</s:layout>
		</s:Group>
	</s:Group>
	
</popup:GenericCRUDContainer>
