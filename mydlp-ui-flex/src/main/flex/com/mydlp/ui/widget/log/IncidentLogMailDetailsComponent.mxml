<?xml version="1.0" encoding="utf-8"?>
<s:Group xmlns:fx="http://ns.adobe.com/mxml/2009" 
		  xmlns:s="library://ns.adobe.com/flex/spark" 
		  xmlns:mx="library://ns.adobe.com/flex/mx"
		  xmlns:domain="com.mydlp.ui.domain.*"
		  creationComplete="onCreationComplete(event)"
		  width="100%"
		  height="100%">
	
	<fx:Script>
		<![CDATA[
			import com.mydlp.ui.skin.RequeueBitmapLabelButton16Skin;
			import com.mydlp.ui.util.LangUtil;
			import com.mydlp.ui.util.SessionUtil;
			
			import mx.collections.ArrayCollection;
			import mx.collections.ListCollectionView;
			import mx.controls.Button;
			import mx.core.FlexGlobals;
			import mx.events.FlexEvent;
			
			import spark.events.IndexChangeEvent;
			
			[Bindable]
			protected var rcpttoList:ArrayCollection = new ArrayCollection();
			[Bindable]
			protected var toList:ArrayCollection = new ArrayCollection();
			[Bindable]
			protected var fromList:ArrayCollection = new ArrayCollection();
			[Bindable]
			protected var ccList:ArrayCollection = new ArrayCollection();
			[Bindable]
			protected var bccList:ArrayCollection = new ArrayCollection();
			
			[Bindable]
			protected var buttonBarDP:ListCollectionView = 
				new ArrayCollection(
					new Array(
						LangUtil.getString('messages', 'log.detail.mail.buttonBar.button.rcpto.label'),
						LangUtil.getString('messages', 'log.detail.mail.buttonBar.button.from.label'),
						LangUtil.getString('messages', 'log.detail.mail.buttonBar.button.to.label'),
						LangUtil.getString('messages', 'log.detail.mail.buttonBar.button.cc.label'),
						LangUtil.getString('messages', 'log.detail.mail.buttonBar.button.bcc.label'),
						LangUtil.getString('messages', 'log.detail.mail.buttonBar.button.requeue.label')
					)
				);
			
			protected var isRequeued:Boolean = false;
			
			protected var requeueButton:RequeueButtonComponent;
			
			protected var naComponent:LogMatcherDetailsNAPopup = new LogMatcherDetailsNAPopup;
			
			public static function startsWith( string:String, pattern:String):Boolean
			{
				string  = string.toLowerCase();
				pattern = pattern.toLowerCase();
				
				return pattern == string.substr( 0, pattern.length );
			}
			
			public function populate():void
			{
				for each (var o:* in incidentLog.destination.split(", "))
				{
					var line:String = o as String;
					if (startsWith(line,"rcpt to: "))
						rcpttoList.addItem(line.substr(9));
					else if (startsWith(line,"to: "))
						toList.addItem(line.substr(4));
					else if (startsWith(line,"from: "))
						fromList.addItem(line.substr(6));
					else if (startsWith(line,"cc: "))
						ccList.addItem(line.substr(4));
					else if (startsWith(line,"bcc: "))
						bccList.addItem(line.substr(5));
				}	
				if(incidentLog.sourceUser != null && incidentLog.sourceUser.length > 0)
					fromList.addItem("<" + incidentLog.sourceUser + ">");
			}
			
			public function indexChange(index:Number):void
			{
				detailGroup.removeAllElements();
				if(index == 0)
				{
					detailHeader.text = LangUtil.getString('messages', 'log.detail.mail.rcpto');
					populateDetails(rcpttoList);
				}
				else if(index == 1)
				{
					detailHeader.text = LangUtil.getString('messages', 'log.detail.mail.from');
					if(SessionUtil.isEnterprise())
						populateDetails(fromList);
					else
						detailGroup.addElement(naComponent);
				}
				else if(index == 2)
				{
					detailHeader.text = LangUtil.getString('messages', 'log.detail.mail.to');
					if(SessionUtil.isEnterprise())
						populateDetails(toList);
					else
						detailGroup.addElement(naComponent);
				}
				else if(index == 3)
				{
					detailHeader.text = LangUtil.getString('messages', 'log.detail.mail.cc');
					if(SessionUtil.isEnterprise())
						populateDetails(ccList);
					else
						detailGroup.addElement(naComponent);
				}
				else if(index == 4)
				{
					detailHeader.text = LangUtil.getString('messages', 'log.detail.mail.bcc');
					if(SessionUtil.isEnterprise())
						populateDetails(bccList);
					else
						detailGroup.addElement(naComponent);
				}
				else if(index == 5)
				{
					detailHeader.text = LangUtil.getString('messages', 'log.detail.mail.requeue');
					populateRequeueStatus();
				}
				else
				{
					detailGroup.removeAllElements();
				}
			}
			
			protected function populateRequeueStatus():void
			{
				if(incidentLog.action != "Q")
				{
					var label:Label = new Label;
					label.text = LangUtil.getString('messages', 'log.container.mail.detail.notapplicable.requeue');
					detailGroup.addElement(label);
				}
				else if(incidentLog.requeueStatus  != null && incidentLog.requeueStatus.isRequeued)
				{
					var label:Label = new Label;
					if (incidentLog.requeueStatus.date != null)
						label.text = LangUtil.getString("messages","log.container.column.policy.requeuedOn") 
							+ ": " + incidentLog.requeueStatus.date.toString();
					else
						label.text = LangUtil.getString("messages","log.container.column.policy.requeueOnProgress");
					detailGroup.addElement(label);
				}
				else if(isRequeued)
				{
					var label:Label = new Label;
					label.text = LangUtil.getString("messages","log.container.column.policy.requeueOnProgress");
					detailGroup.addElement(label);
				}
				else{
					requeueButton = new RequeueButtonComponent;
					requeueButton.includeInLayout = isRequeueVisible();
					requeueButton.visible = isRequeueVisible();
					requeueButton.addEventListener(MouseEvent.CLICK, clickHandler);
					detailGroup.addElement(requeueButton);
				}
			}
			
			protected function clickHandler(event:MouseEvent):void
			{
				FlexGlobals.topLevelApplication.logContainer.requeueIncident(incidentLog);
				incidentLog.requeueStatus.isRequeued = true;
				isRequeued = true;
				requeueButton.visible = false;
				requeueButton.includeInLayout = false;
				detailGroup.removeAllElements();
				populateRequeueStatus();
			}
			
			protected function isRequeueVisible(): Boolean
			{
				if (incidentLog == null) return false;
				if (incidentLog.channel == "M" && incidentLog.action == "Q" && 
					incidentLog.requeueStatus != null && !incidentLog.requeueStatus.isRequeued && !isRequeued)
					return true;
				return false;
			}
			
			protected function populateDetails(itemList:ArrayCollection):void
			{
				for each(var item:String in itemList)
				{
					var label:Label = new Label;
					label.text = item;
					detailGroup.addElement(label);
				}
			}
			
			protected function onCreationComplete(event:FlexEvent):void
			{
				isRequeued = false;
				buttonBar.selectedIndex = 0;
				indexChange(0);
			}
			
		]]>
	</fx:Script>
	
	
	<fx:Declarations>
		<domain:IncidentLog id="incidentLog"/>
	</fx:Declarations>
	
	<s:Rect left="100" right="0" top="0" bottom="0">
		<s:stroke>
			<s:SolidColorStroke color="0x222222"
								alpha="0.90"
								weight="1"/>
		</s:stroke>
	</s:Rect>
	
	<s:VGroup left="100" right="0" top="0" bottom="0"
			  paddingTop="6" paddingBottom="6" paddingLeft="6" paddingRight="6" >
		<s:Label id="detailHeader" fontWeight="bold"/>
		
		<s:Scroller width="100%" height="100%" verticalScrollPolicy="auto" horizontalScrollPolicy="off">
			<s:VGroup id="detailGroup"  width="100%"/>
		</s:Scroller>
	</s:VGroup>
	
	<s:HGroup left="0" bottom="0" top="0" width="100"
			  paddingBottom="0" paddingLeft="0" paddingTop="0" paddingRight="0" gap="0"
			  horizontalAlign="right" verticalAlign="top">
		<s:ButtonBar id="buttonBar" width="100" requireSelection="true"
					 dataProvider="{buttonBarDP}" change="{indexChange(event.newIndex)}" height="100%"/>
	</s:HGroup>
	
</s:Group>
